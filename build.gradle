plugins {
    id 'java'

    // Boot BOM 기반 의존성 버전 관리
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'

    // Spring REST Docs 문서 자동화
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com.woochang.highticket'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    byteBuddyAgent
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    //              == 메인 ==                //

    // MVC Web Starter
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // JPA (MVC)
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Spring Security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // Lombok (컴파일 타임 전용)
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // OAuth2 Client
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // JJWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'

    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // OpenAPI 문서 자동화 + Swagger UI (MVC, 최신: 2.8.11)
    implementation ('org.springdoc:springdoc-openapi-starter-webmvc-api:2.8.11') {
        exclude group: 'org.apache.commons', module: 'commons-lang3'
    }
    implementation 'org.apache.commons:commons-lang3:3.18.0'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.11'

    //              == 런타임 ==                //

    // Postgres JDBC Driver
    runtimeOnly 'org.postgresql:postgresql'

    // JJWT
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    //              == 테스트 ==                //

    // JUnit/Jupiter + Spring Test 통합
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // H2 DB
    testImplementation 'com.h2database:h2'

    // REST Docs (MVC)
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // Security Test
    testImplementation 'org.springframework.security:spring-security-test'

    // IDE/Gradle 런처용
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Mockito inline mock용 에이전트 (테스트 전용)
    // (JDK 최신에서 동적 self-attach 경고 제거 향후 차단 대비)
    byteBuddyAgent 'net.bytebuddy:byte-buddy-agent:1.15.11'

}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
    jvmArgs "-javaagent:${configurations.byteBuddyAgent.singleFile}"
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}

// MapStruct
tasks.withType(JavaCompile).configureEach {
    options.annotationProcessorPath = configurations.annotationProcessor
}

